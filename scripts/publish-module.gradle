apply plugin: 'maven-publish'
apply plugin: 'signing'

tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}
group = PUBLISH_GROUP_ID
version = rootProject.ext["publish_version"]

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                tasks.named("generateMetadataFileForReleasePublication").configure { dependsOn("androidSourcesJar") }

                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version rootProject.ext["publish_version"]

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    artifact("$buildDir/libs/${project.getName()}-${version}.jar")
                }

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Bottom bar in Jetpack Compose with customizable styles & animations'
                    url = 'https://github.com/canopas/AnimatedBottomBarCompose'
                    licenses {
                        license {
                            name = 'License'
                            url = 'https://github.com/canopas/AnimatedBottomBarCompose/blob/master/License'
                        }
                    }
                    developers {
                        developer {
                            id = 'cp-megh-l'
                            name = 'Megh Canopas'
                            email = 'megh.l@canopas.com'
                        }
                        // Add all other devs here...
                    }

                    scm {
                        connection = 'scm:git:github.com/canopas/AnimatedBottomBarCompose.git'
                        developerConnection = 'scm:git:ssh://github.com/canopas/AnimatedBottomBarCompose.git'
                        url = 'https://github.com/canopas/AnimatedBottomBarCompose'
                    }
                }
            }
        }
    }
}
signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )

    sign publishing.publications
}